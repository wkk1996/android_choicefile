plugins {
    id 'com.android.library'
    id 'signing'
    id 'maven-publish'
}
android {
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}
afterEvaluate {
    publishing {

        publications {
            release(MavenPublication) {
                groupId = 'io.github.wkk1996'
                artifactId = 'choiceFile'
                version = VERSION

                // 确保正确引用Android组件（Android库必须）
                from components.release  // 注意：如果是Android库，必须使用这个组件
                pom {
                    name = 'choiceFile'
                    description = 'Android选择文件工具'
                    url = 'https://github.com/wkk1996/android_choicefile'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'wkk'
                            name = 'wkk'
                            email = ossrhEmail
                        }
                    }
                    scm {
                        connection = 'https://github.com/wkk1996/android_choicefile.git'
                        developerConnection = 'https://github.com/wkk1996/android_choicefile.git'
                        url = 'https://github.com/wkk1996/android_choicefile'
                    }
                }
            }
        }
        repositories {
            mavenLocal()
        }
    }
    signing {
        sign publishing.publications.release
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}